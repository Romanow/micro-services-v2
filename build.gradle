plugins {
    id "idea"
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.5.31" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.5.31" apply false
    id "com.gorylenko.gradle-git-properties" version "2.3.1" apply false
    id "org.springframework.boot" version "2.5.6" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

ext {
    springBootVersion = "2.5.6"
    kotlinVersion = "1.5.31"
    validationApiVersion = "2.0.1.Final"
    tracingVersion = "3.3.1"
    gsonVersion = "2.8.5"
    openApiVersion = "1.5.9"
    logstashEncoderVersion = "6.6"
    postgresContainerVersion = "1.16.0"
}

subprojects { ->
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "11"

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"

        implementation "javax.validation:validation-api:$validationApiVersion"
        implementation "org.apache.commons:commons-lang3"
        implementation "com.google.code.gson:gson:$gsonVersion"
    }
}