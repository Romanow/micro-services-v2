plugins {
    id "idea"
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jetbrains.kotlin.kapt" version "1.9.25" apply false
    id "org.jetbrains.kotlin.plugin.jpa" version "1.9.25" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.25" apply false
    id "org.jlleitschuh.gradle.ktlint" version "12.1.1" apply false
    id "org.springframework.boot" version "3.3.4" apply false
    id "com.gorylenko.gradle-git-properties" version "2.4.2" apply false
    id "com.adarshr.test-logger" version "4.0.0" apply false
    id "io.spring.dependency-management" version "1.1.7"
}

ext {
    kotlinVersion = "1.9.25"
    springBootVersion = "3.3.4"
    springCircuitBreakerVersion = "3.1.3"
    validationApiVersion = "2.0.1.Final"
    jwtVersion = "4.4.0"
    openApiVersion = "2.6.0"
    logbookVersion = "2.16.0"
    logstashEncoderVersion = "7.4"
    postgresContainerVersion = "1.20.4"
}

subprojects { ->
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.spring.dependency-management"

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

    repositories {
        mavenCentral()
    }

    ktlint {
        version.set("1.1.1")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-circuitbreaker-dependencies:$springCircuitBreakerVersion"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

        implementation "javax.validation:validation-api:$validationApiVersion"
        implementation "org.apache.commons:commons-lang3"
    }

    processResources {
        filesMatching(["**/properties-from-gradle.properties"]) {
            expand(project.properties)
        }
    }
}
